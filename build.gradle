plugins {
    id 'org.asciidoctor.jvm.convert'
    id 'org.ajoberstar.grgit'
}

repositories {
    jcenter()
}


configurations {
    create("asciidoctor")
}

ext {
    commit = grgit.head()
}

dependencies {
    // TODO demo spring-restdocs
//    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor"
}

asciidoctor {
    if (gradle.startParameter.excludedTaskNames.intersect(["test", "integrationTest"])) {
        enabled = false
    }

    asciidoctorj {
        modules.diagram.version project.asciidocorjDiagramVersion
        options doctype: 'book'
        attributes = [
                // https://asciidoctor.org/docs/user-manual/#highlight-js
                // cd src/docs/asciidoc
                // curl 'https://highlightjs.org/download/' -o highlight.zip .....
                // unzip -d highlight highlight.zip
                // mv highlight/highlight.pack.js highlight/highlight.min.js
                // for css in highlight/style/*.css; do mv $css highlight/style/$(basename "$css" .css).min.css; done
                'source-highlighter': 'highlight.js',
                'highlightjs-theme' : 'hl-dark-mode-aware', // try other themes at https://highlightjs.org/static/demo/
                'stylesheet'        : 'dark-mode-aware.css',

                // to declare in subtree's adoc files
                // https://github.com/asciidoctor/asciidoctor-gradle-plugin/issues/292
//                'highlightjsdir'    : '{gradle-relative-srcdir}/highlight',
//                'stylesdir'         : '{gradle-relative-srcdir}',

                'linkcss'           : 'true',
//                'copycss'           : 'true',
                'encoding'          : 'utf-8'
        ]
        attributeProvider {
            [
                    'build-timestamp': commit.dateTime,
                    'revnumber'      : commit.id,
                    'commit'         : commit.id
            ]
        }
    }

    resources {
        from(sourceDir) {
            include 'images/**'
            include '*.css'
            include 'highlight/**'
        }
    }

    sources {
        include 'plantuml-test.adoc'
        include 'demo.adoc'
        include 'nested/*.adoc'
    }
    baseDirFollowsSourceDir()

    inputs.files("${sourceDir}/asciidark.css")
    inputs.dir("${sourceDir}/highlight")

    configurations("asciidoctor")
    // enforce asciidoc processing
//     outputs.upToDateWhen { false }
}
